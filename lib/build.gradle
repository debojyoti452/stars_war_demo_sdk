plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'maven-publish'
    id 'signing'
}

android {
    def globalConfiguration = rootProject.extensions.getByName("ext")

    compileSdk globalConfiguration["androidCompileSDK"]

    defaultConfig {
        minSdk globalConfiguration["androidMinSDK"]
        targetSdk globalConfiguration["androidCompileSDK"]

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        dev {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    flavorDimensions "default"

    productFlavors {
        develop {
            buildConfigField "String", 'STARWARS_BASE_URL', "\"" + getLocalPropertiesValue("starwars.dev.baseurl") + "\""
        }
    }
}

dependencies {
    def ext = rootProject.ext

    implementation(
            ext.libDependencies.kotlin,
            ext.libDependencies.kotlinKtx,
            ext.libDependencies.coroutines,
            ext.libDependencies.coroutinesAndroid,
            ext.libDependencies.timber,
            ext.libDependencies.gson,

            ext.libDependencies.retrofit,
            ext.libDependencies.retrofitConversion,
            ext.libDependencies.okHttp,
            ext.libDependencies.okHttpLoggingInterceptor,
            ext.libDependencies.retrofitCoroutinesAdapter,
    )

    testImplementation(
            ext.testDependencies.junit,
            ext.testDependencies.truth,
            ext.testDependencies.mockk,
    )
}

afterEvaluate {
    publishing {
        publications {
            dev(MavenPublication) {
                groupId = 'com.example.starwarsdk'
                artifactId = 'starwars_sdk'
                version = 3

//                if (project.plugins.findPlugin("com.android.library")) {
//                    from components.dev
//                } else {
//                    from components.java
//                }
            }
        }
    }
}


/**
 * get value from local.properties
 *
 * @return value of the key
 */
def getLocalPropertiesValue(key) {
    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
    return properties.getProperty(key)
}
